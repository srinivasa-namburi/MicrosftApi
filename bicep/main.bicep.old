/*
Copyright (c) Microsoft. All rights reserved.
Licensed under the MIT license. See LICENSE file in the project root for full license information.

Bicep template for deploying Project Vico Azure resources.
*/

@description('Deployment name')
param deploymentName string = 'vico-deployment'

@description('SKU for the Azure App Service plan for webapi')
@allowed([ 'B1', 'S1', 'S2', 'S3', 'P1V3', 'P2V3', 'I1V2', 'I2V2' ])
param webapiASPsku string = 'B1'

@description('SKU for the Azure App Service plan for plugin functions')
@allowed([ 'B1', 'S1', 'S2', 'S3', 'P1V3', 'P2V3', 'I1V2', 'I2V2' ])
param pluginASPsku string = 'B1'

@description('Underlying AI service')
@allowed([
  'AzureOpenAI'
  'OpenAI'
])
param aiService string = 'AzureOpenAI'

@description('Model to use for chat completions')
param completionModel string = 'gpt-35-turbo'

@description('Model to use for text embeddings')
param embeddingModel string = 'text-embedding-ada-002'

@description('Completion model tokens per Minute Rate Limit (thousands)')
param completionModelTPM int = 120

@description('Embedding model tokens per Minute Rate Limit (thousands)')
param embeddingModelTPM int = 120

@description('Completion model the task planner should use')
param plannerModel string = 'gpt-35-turbo'

@description('Azure OpenAI endpoint to use (Azure OpenAI only)')
param aiEndpoint string = ''

@secure()
@description('Azure OpenAI or OpenAI API key')
param aiApiKey string = ''

@description('Azure AD client ID for the backend web API')
param webApiClientId string = ''

@description('Azure AD tenant ID for authenticating users')
param azureAdTenantId string = ''

@description('Azure AD cloud instance for authenticating users')
param azureAdInstance string = environment().authentication.loginEndpoint

@description('Whether to deploy a new Azure OpenAI instance')
param deployNewAzureOpenAI bool = false

@description('Whether to deploy Cosmos DB for persistent chat storage')
param deployCosmosDB bool = true

@description('What method to use to persist embeddings')
@allowed([
  'Volatile'
  'AzureCognitiveSearch'
  // 'Qdrant' - not implemented
  // 'Postgres' - not implemented
])
param memoryStore string = 'Volatile'

@description('Region for the resources')
/*
@allowed([
  'eastus'
  'australiaeast'
  'canadaeast'
  'eastus2'
  'francecentral'
  'japaneast'
  'northcentralus'
  'swedencentral'
  'switzerlandnorth'
  'uksouth'
  'westeurope'
])
*/
param location string = resourceGroup().location

@description('Tags to apply to all resources')
param tags object = {
  project: 'vico'
  environment: 'dev'
  version: '0.1'
}

@description('Region for the webapp frontend')
@allowed([
  'westus2'
  'centralus'
  'eastus2'
  'westeurope'
  'eastasia'
  'eastasiastage'
])
param webappLocation string = 'westus2'

@description('Function apps to deploy')
param functionAppNameArray array = [
  'DocQnA'
  'GeographicalData'
  // 'Earthquake'
  // 'Weather'
]

@description('Names of private DNS zones to deploy')
param privateDnsZoneNames array = [
  'privatelink.search.windows.net'
]

@description('Maximum capacity for the application gateway')
param appgwMaxCapacity int = 10

@description('Hash of the resource group ID')
var rgIdHash = uniqueString(resourceGroup().id)

@description('Deployment name unique to resource group')
var uniqueName = rgIdHash


// Open AI deployment
resource openAI 'Microsoft.CognitiveServices/accounts@2023-05-01' = if (deployNewAzureOpenAI) {
  name: 'openai-vico-${uniqueName}'
  location: location
  kind: 'OpenAI'
  sku: {
    name: 'S0'
  }
  properties: {
    customSubDomainName: toLower(uniqueName)
  }
  tags: tags
}

resource openAI_completionModel 'Microsoft.CognitiveServices/accounts/deployments@2023-05-01' = if (deployNewAzureOpenAI) {
  parent: openAI
  name: completionModel
  properties: {
    model: {
      format: 'OpenAI'
      name: completionModel
    }
  }
  sku: {
    name: 'Standard'
    capacity: completionModelTPM
  }
}

resource openAI_embeddingModel 'Microsoft.CognitiveServices/accounts/deployments@2023-05-01' = if (deployNewAzureOpenAI) {
  parent: openAI
  name: embeddingModel
  properties: {
    model: {
      format: 'OpenAI'
      name: embeddingModel
    }
  }
    sku: {
      name: 'Standard'
      capacity: embeddingModelTPM
    }
  dependsOn: [// This "dependency" is to create models sequentially because the resource
    openAI_completionModel // provider does not support parallel creation of models properly.
  ]
}

// VNET Project Vico

resource skNsg 'Microsoft.Network/networkSecurityGroups@2023-04-01' = {
  name: 'nsg-sk-${uniqueName}'
  location: location
  properties: {
    securityRules: [
    ]
  }
}

resource pgNsg 'Microsoft.Network/networkSecurityGroups@2023-04-01' = {
  name: 'nsg-pg-${uniqueName}'
  location: location
  properties: {
    securityRules: [
    ]
  }
}

resource peNsg 'Microsoft.Network/networkSecurityGroups@2023-04-01' = {
  name: 'nsg-pe-${uniqueName}'
  location: location
  properties: {
    securityRules: [
    ]
  }
}

resource stoNsg 'Microsoft.Network/networkSecurityGroups@2023-04-01' = {
  name: 'nsg-sto-${uniqueName}'
  location: location
  properties: {
    securityRules: [
    ]
  }
}

resource aiNsg 'Microsoft.Network/networkSecurityGroups@2023-04-01' = {
  name: 'nsg-ai-${uniqueName}'
  location: location
  properties: {
    securityRules: [
    ]
  }
}

resource webNsg 'Microsoft.Network/networkSecurityGroups@2023-04-01' = {
  name: 'nsg-web-${uniqueName}'
  location: location
  properties: {
    securityRules: [
    ]
  }
}

resource serNsg 'Microsoft.Network/networkSecurityGroups@2023-04-01' = {
  name: 'nsg-ser-${uniqueName}'
  location: location
  properties: {
    securityRules: [
    ]
  }
}

resource virtualNetwork 'Microsoft.Network/virtualNetworks@2023-04-01' = {
  name: 'vnet-${uniqueName}'
  location: location
  properties: {
    addressSpace: {
      addressPrefixes: [
        '10.0.0.0/16'
      ]
    }
    subnets: [
      {
        name: 'ApplicationGatewaySubnet'
        properties: {
          addressPrefix: '10.0.0.0/24'
          /*serviceEndpoints: [
            {
              service: 'Microsoft.Web'
              locations: [
                '*'
              ]
            }
          ]*/
          privateEndpointNetworkPolicies: 'Enabled'
          privateLinkServiceNetworkPolicies: 'Enabled'
        }
      }
      {
        name: 'SemanticKernelSubnet'
        properties: {
          addressPrefix: '10.0.1.0/24'
          networkSecurityGroup: {
            id: skNsg.id
          }
          /*serviceEndpoints: [
            {
              service: 'Microsoft.Web'
              locations: [
                '*'
              ]
            }
          ]*/
          delegations: [
            {
              name: 'delegation'
              properties: {
                serviceName: 'Microsoft.Web/serverfarms'
              }
            }
          ]
          privateEndpointNetworkPolicies: 'Enabled'
          privateLinkServiceNetworkPolicies: 'Enabled'
        }
      }
      {
        name: 'PluginsSubnet'
        properties: {
          addressPrefix: '10.0.2.0/24'
          serviceEndpoints: []
          networkSecurityGroup: {
            id: pgNsg.id
          }
          delegations: [
            {
              name: 'delegation'
              properties: {
                serviceName: 'Microsoft.Web/serverfarms'
              }
            }
          ]
          privateEndpointNetworkPolicies: 'Enabled'
          privateLinkServiceNetworkPolicies: 'Enabled'
        }
      }
      {
        name: 'WebSubnet'
        properties: {
          addressPrefix: '10.0.3.0/24'
          serviceEndpoints: []
          networkSecurityGroup: {
            id: webNsg.id
          }
          privateEndpointNetworkPolicies: 'Enabled'
          privateLinkServiceNetworkPolicies: 'Enabled'
        }
      }
      {
        name: 'StorageSubnet'
        properties: {
          addressPrefix: '10.0.4.0/27'
          serviceEndpoints: []
          networkSecurityGroup: {
            id: stoNsg.id
          }
          privateEndpointNetworkPolicies: 'Enabled'
          privateLinkServiceNetworkPolicies: 'Enabled'
        }
      }
      {
        name: 'AISubnet'
        properties: {
          addressPrefix: '10.0.4.32/27'
          serviceEndpoints: []
          networkSecurityGroup: {
            id: aiNsg.id
          }
          privateEndpointNetworkPolicies: 'Enabled'
          privateLinkServiceNetworkPolicies: 'Enabled'
        }
      }
      {
        name: 'ServiceSubnet'
        properties: {
          addressPrefix: '10.0.4.64/27'
          serviceEndpoints: []
          networkSecurityGroup: {
            id: serNsg.id
          }
          privateEndpointNetworkPolicies: 'Enabled'
          privateLinkServiceNetworkPolicies: 'Enabled'
        }
      }
    ]
  }
}

// Deploy Key Vault

resource keyvault 'Microsoft.KeyVault/vaults@2022-07-01' = {
  name: 'kv-vico-${uniqueName}'
  location: location
  tags: tags
  properties: {
    createMode: 'default' // This is required to recover a soft-deleted key vault
    enabledForTemplateDeployment: true
    enableRbacAuthorization: true
    enableSoftDelete: true
    networkAcls: {
      bypass: 'AzureServices'
      defaultAction: 'Deny'
      ipRules: [
      ]
    }
    publicNetworkAccess: 'disabled' // Only traffic from private endpoint
    sku: {
      family: 'A'
      name: 'standard'
    }
    accessPolicies: []
    tenantId: subscription().tenantId
  }
}


resource KVPrivateEndpoint 'Microsoft.Network/privateEndpoints@2021-08-01' = {
  name: '${keyvault.name}-pl'
  tags: tags
  location: location
  properties: {
    subnet: {
      id: virtualNetwork.properties.subnets[6].id
    }
    privateLinkServiceConnections: [
      {
        name: '${keyvault.name}-pl'
        properties: {
          privateLinkServiceId: keyvault.id
          groupIds: ['vault']
        }
      }
    ]
  }
}

resource KVEndpointAEntry 'Microsoft.Network/privateDnsZones/A@2020-06-01' = {
  name: keyvault.name
  parent: privateDnsZones[8]
  properties: {
    ttl: 3600
    aRecords: [
      {
        ipv4Address: KVPrivateEndpoint.properties.customDnsConfigs[0].ipAddresses[0]
      }
    ]
  }
}

// Deploy Application Gateway

resource publicIPAddresses 'Microsoft.Network/publicIPAddresses@2021-05-01' = {
  name: 'pip-appgw-${uniqueName}'
  location: location
  tags: tags
  sku: {
    name: 'Standard'
  }
  properties: {
    publicIPAddressVersion: 'IPv4'
    publicIPAllocationMethod: 'Static'
    idleTimeoutInMinutes: 4
  }
}

resource umsiAppGW 'Microsoft.ManagedIdentity/userAssignedIdentities@2023-01-31' = {
  name: 'umsi-appgw-${uniqueName}'
  location: location
  tags: tags
}

resource kvAppGWAccess 'Microsoft.KeyVault/vaults/accessPolicies@2022-07-01' = {
  name: 'add'
  parent: keyvault
  properties: {
    accessPolicies: [
      {
        objectId: umsiAppGW.properties.principalId
        permissions: {
          certificates: [
            'get'
            'list'
          ]
          keys: []
          secrets: []
          storage: []
        }
        tenantId: subscription().tenantId
      }
    ]
  }
}

resource applicationGateWay 'Microsoft.Network/applicationGateways@2023-04-01' = {
  name: 'appgw-${uniqueName}'
  location: location
  tags: tags
  identity: {
    type: 'UserAssigned'
    userAssignedIdentities: {
      '${umsiAppGW.id}': {}
    }
  }
  properties:{
    sku: {
      name: 'WAF_v2'
      tier: 'WAF_v2'
    }
    gatewayIPConfigurations: [
      {
        name: 'appgwIpConfig'
        properties: {
          subnet: {
            id: virtualNetwork.properties.subnets[0].id
          }
        }
      }
    ]
    frontendIPConfigurations: [
      {
        name: 'appgwPublicFrontendIPv4'
        properties: {
          publicIPAddress: {
            id: publicIPAddresses.id
          }
        }
      }
      {
        name: 'appgwPrivateFrontendIPv4'
        properties: {
          subnet: {
            id: virtualNetwork.properties.subnets[0].id
          }
          privateIPAddress: '10.0.0.4'
          privateIPAllocationMethod: 'Static'
        }
      }
    ]
    frontendPorts: [
      {
        name: 'port_40443'
        properties: {
          port: 40443
        }
      }
    ]
    backendAddressPools: [
      {
        name: 'webapiBackendPool'
        properties: {
          backendAddresses: [
            {
              fqdn: SemanticKernelWeb.properties.defaultHostName
            }
          ]
        }
      }
    ]
    backendHttpSettingsCollection: [
      {
        name: 'webapiBackendHttpSettings'
        properties: {
          port: 40443
          protocol: 'Http'
          cookieBasedAffinity: 'Enabled'
          requestTimeout: 20
          affinityCookieName: 'ApplicationGatewayAffinity'
        }
      }
    ]
    httpListeners: [
      {
        name: 'webapiHttpListener'
        properties: {
          frontendIPConfiguration: {
            id: resourceId('Microsoft.Network/applicationGateways/frontendIPConfigurations', 'appgw-${uniqueName}', 'appgwPublicFrontendIPv4')
          }
          frontendPort: {
            id: resourceId('Microsoft.Network/applicationGateways/frontendPorts', 'appgw-${uniqueName}', 'port_40443')
          }
          protocol: 'Http'
          requireServerNameIndication: false
          hostName: SemanticKernelWeb.properties.defaultHostName

        }
      }
    ]
    requestRoutingRules: [
      {
        name: 'webapiRoutingRule-40443'
        properties: {
          backendAddressPool: {
            id: resourceId('Microsoft.Network/applicationGateways/backendAddressPools', 'appgw-${uniqueName}', 'webapiBackendPool')
          }
          backendHttpSettings: {
            id: resourceId('Microsoft.Network/applicationGateways/backendHttpSettingsCollection', 'appgw-${uniqueName}', 'webapiBackendHttpSettings')
          }
          httpListener: {
            id: resourceId('Microsoft.Network/applicationGateways/httpListeners', 'appgw-${uniqueName}', 'webapiHttpListener')
          }
          priority: 100
          ruleType: 'Basic'
        }
      }
    ]
    enableHttp2: true
    sslCertificates: [
    ]
    probes: [] // TODO add probe webapi
    autoscaleConfiguration: {
      minCapacity: 0
      maxCapacity: appgwMaxCapacity
    }
    firewallPolicy: {
      id: appgwWafPolicy.id
    }
  }
}

resource appgwWafPolicy 'Microsoft.Network/ApplicationGatewayWebApplicationFirewallPolicies@2023-04-01' = {
  name: 'wafpolicy-${uniqueName}'
  location: location
  tags: tags
  properties: {
    policySettings: {
      state: 'Enabled'
      mode: 'Prevention'
      fileUploadLimitInMb: 100
      requestBodyCheck: true
      maxRequestBodySizeInKb: 128
    }
    managedRules: {
      exclusions: []
      managedRuleSets: [
        {
          ruleSetType: 'OWASP'
          ruleSetVersion: '3.2'
          ruleGroupOverrides: null
        }
        {
          ruleSetType: 'Microsoft_BotManagerRuleSet'
          ruleSetVersion: '0.1'
          ruleGroupOverrides: null
        }
      ]
    }
  }
}

// Deploy private DNS Zones

resource privateDnsZones 'Microsoft.Network/privateDnsZones@2020-06-01' = [for dnsZoneName in privateDnsZoneNames: {
  name: dnsZoneName
  location: 'global'
  tags: tags
}]



// Deploy Log Analytics workspace
resource logAnalyticsWorkspace 'Microsoft.OperationalInsights/workspaces@2022-10-01' = {
  name: 'la-vico-${uniqueName}'
  location: location
  tags: tags
  properties: {
    sku: {
      name: 'PerGB2018'
    }
    retentionInDays: 90
    features: {
      searchVersion: 1
      legacy: 0
      enableLogAccessUsingOnlyResourcePermissions: true
    }
  }
}

// Deploy Application Insights
resource appInsights 'Microsoft.Insights/components@2020-02-02' = {
  name: 'appins-vico-${uniqueName}'
  location: location
  kind: 'string'
  tags: tags
  properties: {
    Application_Type: 'web'
    WorkspaceResourceId: logAnalyticsWorkspace.id
  }
}

// Deploy Cognitive Search

resource azureCognitiveSearch 'Microsoft.Search/searchServices@2022-09-01' = if (memoryStore == 'AzureCognitiveSearch') {
  name: 'acs-vico-${uniqueName}'
  location: location
  sku: {
    name: 'basic'
  }
  properties: {
    replicaCount: 1
    partitionCount: 1
  }
}

resource CSPrivateEndpoint 'Microsoft.Network/privateEndpoints@2021-08-01' = if (memoryStore == 'AzureCognitiveSearch') {
  name: '${azureCognitiveSearch.name}-pl'
  tags: tags
  location: location
  properties: {
    subnet: {
      id: virtualNetwork.properties.subnets[5].id
    }
    privateLinkServiceConnections: [
      {
        name: '${azureCognitiveSearch.name}-pl'
        properties: {
          privateLinkServiceId: azureCognitiveSearch.id
          groupIds: ['searchService']
        }
      }
    ]
  }
}

resource CSEndpointAEntry 'Microsoft.Network/privateDnsZones/A@2020-06-01' = if (memoryStore == 'AzureCognitiveSearch') {
  name: azureCognitiveSearch.name
  parent: privateDnsZones[0]
  properties: {
    ttl: 3600
    aRecords: [
      {
        ipv4Address: CSPrivateEndpoint.properties.customDnsConfigs[0].ipAddresses[0]
      }
    ]
  }
}


// Deploy SemanticKernel Infrastructure

resource SemanticKernelASP 'Microsoft.Web/serverfarms@2022-09-01' = {
  name: 'asp-${uniqueName}-webapi'
  location: location
  kind: 'app'
  sku: {
    name: webapiASPsku
  }
}

resource SemanticKernelWeb 'Microsoft.Web/sites@2022-09-01' = {
  name: 'app-${uniqueName}-webapi'
  location: location
  kind: 'app'
  tags: tags
  properties: {
    serverFarmId: SemanticKernelASP.id
    httpsOnly: true
    virtualNetworkSubnetId: virtualNetwork.properties.subnets[1].id
    siteConfig: {
      healthCheckPath: '/healthz'
    }
  }
}

resource SemanticKernelWebConfig 'Microsoft.Web/sites/config@2022-09-01' = {
  parent: SemanticKernelWeb
  name: 'web'
  properties: {
    alwaysOn: false
    cors: {
      allowedOrigins: [
        'http://localhost:3000' // replace with frontend URL 
        'https://localhost:3000' // replace with frontend URL
      ]
      supportCredentials: true
    }
    detailedErrorLoggingEnabled: true
    minTlsVersion: '1.2'
    netFrameworkVersion: 'v6.0'
    use32BitWorkerProcess: false
    vnetRouteAllEnabled: true
    webSocketsEnabled: true
    appSettings: [
      {
        name: 'AIService:Type'
        value: aiService
      }
      {
        name: 'AIService:Endpoint'
        value: deployNewAzureOpenAI ? openAI.properties.endpoint : aiEndpoint
      }
      {
        name: 'AIService:Key'
        value: deployNewAzureOpenAI ? openAI.listKeys().key1 : aiApiKey
      }
      {
        name: 'AIService:Models:Completion'
        value: completionModel
      }
      {
        name: 'AIService:Models:Embedding'
        value: embeddingModel
      }
      {
        name: 'AIService:Models:Planner'
        value: plannerModel
      }
      {
        name: 'Authentication:Type'
        value: 'AzureAd'
      }
      {
        name: 'Authentication:AzureAd:Instance'
        value: azureAdInstance
      }
      {
        name: 'Authentication:AzureAd:TenantId'
        value: azureAdTenantId
      }
      {
        name: 'Authentication:AzureAd:ClientId'
        value: webApiClientId
      }
      {
        name: 'Authentication:AzureAd:Scopes'
        value: 'access_as_user'
      }
      {
        name: 'ChatStore:Type'
        value: deployCosmosDB ? 'cosmos' : 'volatile'
      }
      {
        name: 'ChatStore:Cosmos:Database'
        value: 'CopilotChat'
      }
      {
        name: 'ChatStore:Cosmos:ChatSessionsContainer'
        value: 'chatsessions'
      }
      {
        name: 'ChatStore:Cosmos:ChatMessagesContainer'
        value: 'chatmessages'
      }
      {
        name: 'ChatStore:Cosmos:ChatMemorySourcesContainer'
        value: 'chatmemorysources'
      }
      {
        name: 'ChatStore:Cosmos:ChatParticipantsContainer'
        value: 'chatparticipants'
      }
      {
        name: 'ChatStore:Cosmos:ConnectionString'
        value: deployCosmosDB ? cosmosAccount.listConnectionStrings().connectionStrings[0].connectionString : ''
      }
      {
        name: 'MemoryStore:Type'
        value: memoryStore
      }
      /*{
        name: 'MemoryStore:Qdrant:Host'
        value: memoryStore == 'Qdrant' ? 'https://${appServiceQdrant.properties.defaultHostName}' : ''
      }
      {
        name: 'MemoryStore:Qdrant:Port'
        value: '443'
      }
      */
      {
        name: 'MemoryStore:AzureCognitiveSearch:UseVectorSearch'
        value: 'true'
      }
      {
        name: 'MemoryStore:AzureCognitiveSearch:Endpoint'
        value: memoryStore == 'AzureCognitiveSearch' ? 'https://${azureCognitiveSearch.name}.search.windows.net' : ''
      }
      {
        name: 'MemoryStore:AzureCognitiveSearch:Key'
        value: memoryStore == 'AzureCognitiveSearch' ? azureCognitiveSearch.listAdminKeys().primaryKey : ''
      }
      /*{
        name: 'MemoryStore:Postgres:ConnectionString'
        value: memoryStore == 'Postgres' ? 'Host=${postgreServerGroup.properties.serverNames[0].fullyQualifiedDomainName}:5432;Username=citus;Password=${sqlAdminPassword};Database=citus' : ''
      }*/
      {
        name: 'AzureSpeech:Region'
        value: location
      }
      {
        name: 'AllowedOrigins'
        value: '[*]' // Defer list of allowed origins to the Azure service app's CORS configuration
      }
      {
        name: 'Kestrel:Endpoints:Https:Url'
        value: 'https://localhost:443'
      }
      {
        name: 'Logging:LogLevel:Default'
        value: 'Warning'
      }
      {
        name: 'Logging:LogLevel:CopilotChat.WebApi'
        value: 'Warning'
      }
      {
        name: 'Logging:LogLevel:Microsoft.SemanticKernel'
        value: 'Warning'
      }
      {
        name: 'Logging:LogLevel:Microsoft.AspNetCore.Hosting'
        value: 'Warning'
      }
      {
        name: 'Logging:LogLevel:Microsoft.Hosting.Lifetimel'
        value: 'Warning'
      }
      {
        name: 'ApplicationInsights:ConnectionString'
        value: appInsights.properties.ConnectionString
      }
      {
        name: 'APPLICATIONINSIGHTS_CONNECTION_STRING'
        value: appInsights.properties.ConnectionString
      }
      {
        name: 'ApplicationInsightsAgent_EXTENSION_VERSION'
        value: '~2'
      }
    ]
  }
}

resource appInsightExtension 'Microsoft.Web/sites/siteextensions@2022-09-01' = {
  parent: SemanticKernelWeb
  name: 'Microsoft.ApplicationInsights.AzureWebSites'
  dependsOn: [ SemanticKernelWebConfig ]
}

resource SKPrivateEndpoint 'Microsoft.Network/privateEndpoints@2021-08-01' = {
  name: '${SemanticKernelWeb.name}-pl'
  tags: tags
  location: location
  properties: {
    subnet: {
      id: virtualNetwork.properties.subnets[3].id
    }
    privateLinkServiceConnections: [
      {
        name: '${SemanticKernelWeb.name}-pl'
        properties: {
          privateLinkServiceId: SemanticKernelWeb.id
          groupIds: ['sites']
        }
      }
    ]
  }
}

resource SKEndpointAEntry 'Microsoft.Network/privateDnsZones/A@2020-06-01' = {
  name: staticWebApp.name
  parent: privateDnsZones[3]
  properties: {
    ttl: 3600
    aRecords: [
      {
        ipv4Address: SKPrivateEndpoint.properties.customDnsConfigs[0].ipAddresses[0]
      }
    ]
  }
}

// Deploy Plugins Infrastructure

resource PluginsKernelASP 'Microsoft.Web/serverfarms@2022-09-01' = {
  name: 'asp-${uniqueName}-plugins'
  location: location
  kind: 'app'
  sku: {
    name: pluginASPsku
  }
}

resource storageAccount 'Microsoft.Storage/storageAccounts@2022-09-01' = [for functionAppName in functionAppNameArray: {
  name: 'sta${toLower(functionAppName)}'
  location: location
  sku: {
    name: 'Standard_LRS'
  }
  kind: 'StorageV2'
  properties: {
    supportsHttpsTrafficOnly: true
    defaultToOAuthAuthentication: true
    /*publicNetworkAccess: 'Disabled'
    networkAcls: {
      bypass: 'None'
      defaultAction: 'Deny'
    }*/
  }
}]

resource SABlobPrivateEndpoint 'Microsoft.Network/privateEndpoints@2021-08-01' = [for (functionAppName, i) in functionAppNameArray: {
  name: '${storageAccount[i].name}-blob-pl'
  tags: tags
  location: location
  properties: {
    subnet: {
      id: virtualNetwork.properties.subnets[4].id
    }
    privateLinkServiceConnections: [
      {
        name: '${storageAccount[i].name}-blob-pl'
        properties: {
          privateLinkServiceId: storageAccount[i].id
          groupIds: ['blob']
        }
      }
    ]
  }
}]

resource SABlobEndpointAEntry 'Microsoft.Network/privateDnsZones/A@2020-06-01' = [for (functionAppName, i) in functionAppNameArray: {
  name: 'sta${toLower(functionAppName)}'
  parent: privateDnsZones[4]
  properties: {
    ttl: 3600
    aRecords: [
      {
        ipv4Address: SABlobPrivateEndpoint[i].properties.customDnsConfigs[0].ipAddresses[0]
      }
    ]
  }
}]

resource SAFilePrivateEndpoint 'Microsoft.Network/privateEndpoints@2021-08-01' = [for (functionAppName, i) in functionAppNameArray: {
  name: '${storageAccount[i].name}-file-pl'
  tags: tags
  location: location
  properties: {
    subnet: {
      id: virtualNetwork.properties.subnets[4].id
    }
    privateLinkServiceConnections: [
      {
        name: '${storageAccount[i].name}-file-pl'
        properties: {
          privateLinkServiceId: storageAccount[i].id
          groupIds: ['file']
        }
      }
    ]
  }
}]

resource SAFileEndpointAEntry 'Microsoft.Network/privateDnsZones/A@2020-06-01' = [for (functionAppName, i) in functionAppNameArray: {
  name: 'sta${toLower(functionAppName)}'
  parent: privateDnsZones[5]
  properties: {
    ttl: 3600
    aRecords: [
      {
        ipv4Address: SAFilePrivateEndpoint[i].properties.customDnsConfigs[0].ipAddresses[0]
      }
    ]
  }
}]
resource SATablePrivateEndpoint 'Microsoft.Network/privateEndpoints@2021-08-01' = [for (functionAppName, i) in functionAppNameArray: {
  name: '${storageAccount[i].name}-table-pl'
  tags: tags
  location: location
  properties: {
    subnet: {
      id: virtualNetwork.properties.subnets[4].id
    }
    privateLinkServiceConnections: [
      {
        name: '${storageAccount[i].name}-table-pl'
        properties: {
          privateLinkServiceId: storageAccount[i].id
          groupIds: ['table']
        }
      }
    ]
  }
}]
resource SATableEndpointAEntry 'Microsoft.Network/privateDnsZones/A@2020-06-01' = [for (functionAppName, i) in functionAppNameArray: {
  name: 'sta${toLower(functionAppName)}'
  parent: privateDnsZones[6]
  properties: {
    ttl: 3600
    aRecords: [
      {
        ipv4Address: SATablePrivateEndpoint[i].properties.customDnsConfigs[0].ipAddresses[0]
      }
    ]
  }
}]
resource SAQueuePrivateEndpoint 'Microsoft.Network/privateEndpoints@2021-08-01' = [for (functionAppName, i) in functionAppNameArray: {
  name: '${storageAccount[i].name}-queue-pl'
  tags: tags
  location: location
  properties: {
    subnet: {
      id: virtualNetwork.properties.subnets[4].id
    }
    privateLinkServiceConnections: [
      {
        name: '${storageAccount[i].name}-queue-pl'
        properties: {
          privateLinkServiceId: storageAccount[i].id
          groupIds: ['queue']
        }
      }
    ]
  }
}]
resource SAQueueEndpointAEntry 'Microsoft.Network/privateDnsZones/A@2020-06-01' = [for (functionAppName, i) in functionAppNameArray: {
  name: 'sta${toLower(functionAppName)}'
  parent: privateDnsZones[7]
  properties: {
    ttl: 3600
    aRecords: [
      {
        ipv4Address: SAQueuePrivateEndpoint[i].properties.customDnsConfigs[0].ipAddresses[0]
      }
    ]
  }
}]

resource functionApp 'Microsoft.Web/sites@2022-09-01' = [for (functionAppName, i) in functionAppNameArray: {
  name: 'func-vico-${functionAppName}-${uniqueName}'
  location: location
  kind: 'functionapp'
  identity: {
    type: 'SystemAssigned'
  }
  properties: {
    serverFarmId: PluginsKernelASP.id
    httpsOnly: true
    virtualNetworkSubnetId: virtualNetwork.properties.subnets[2].id
    siteConfig: {
      appSettings: [
        {
          name: 'AzureWebJobsStorage'
          value: 'DefaultEndpointsProtocol=https;AccountName=sta${toLower(functionAppName)};EndpointSuffix=${environment().suffixes.storage};AccountKey=${storageAccount[i].listKeys().keys[0].value}'
        }
        {
          name: 'WEBSITE_CONTENTAZUREFILECONNECTIONSTRING'
          value: 'DefaultEndpointsProtocol=https;AccountName=sta${toLower(functionAppName)};EndpointSuffix=${environment().suffixes.storage};AccountKey=${storageAccount[i].listKeys().keys[0].value}'
        }
        {
          name: 'FUNCTIONS_EXTENSION_VERSION'
          value: '~4'
        }
        {
          name: 'WEBSITE_NODE_DEFAULT_VERSION'
          value: '~14'
        }
        {
          name: 'APPINSIGHTS_INSTRUMENTATIONKEY'
          value: appInsights.properties.InstrumentationKey
        }
        {
          name: 'FUNCTIONS_WORKER_RUNTIME'
          value: 'dotnet'
        }
        /*{
          name: 'WEBSITE_VNET_ROUTE_ALL'
          value: '1'
        }
        {
          name: 'WEBSITE_CONTENTOVERVNET'
          value: '1'
        }*/
      ]
      ftpsState: 'FtpsOnly'
      minTlsVersion: '1.2'
    }
  }
}]

resource FAPrivateEndpoint 'Microsoft.Network/privateEndpoints@2021-08-01' = [for (functionAppName, i) in functionAppNameArray: {
  name: 'func-${functionAppName}-pl'
  tags: tags
  location: location
  properties: {
    subnet: {
      id: virtualNetwork.properties.subnets[3].id
    }
    privateLinkServiceConnections: [
      {
        name: '${functionAppName}-pl'
        properties: {
          privateLinkServiceId: functionApp[i].id
          groupIds: ['sites']
        }
      }
    ]
  }
}]

resource FAEndpointAEntry 'Microsoft.Network/privateDnsZones/A@2020-06-01' = [for (functionAppName, i) in functionAppNameArray: {
  name: functionAppName
  parent: privateDnsZones[3]
  properties: {
    ttl: 3600
    aRecords: [
      {
        ipv4Address: FAPrivateEndpoint[i].properties.customDnsConfigs[0].ipAddresses[0]
      }
    ]
  }
}]

// Deploy static webapp frontend
resource staticWebApp 'Microsoft.Web/staticSites@2022-09-01' = {
  name: 'swa-vico-${uniqueName}'
  location: webappLocation
  properties: {
    provider: 'None'
  }
  sku: {
    name: 'Standard'
    tier: 'Standard'
  }
}

resource SWAPrivateEndpoint 'Microsoft.Network/privateEndpoints@2021-08-01' = {
  name: '${staticWebApp.name}-pl'
  tags: tags
  location: location
  properties: {
    subnet: {
      id: virtualNetwork.properties.subnets[3].id
    }
    privateLinkServiceConnections: [
      {
        name: '${staticWebApp.name}-pl'
        properties: {
          privateLinkServiceId: staticWebApp.id
          groupIds: ['staticSites']
        }
      }
    ]
  }
}

resource SAWEndpointAEntry 'Microsoft.Network/privateDnsZones/A@2020-06-01' = {
  name: staticWebApp.name
  parent: privateDnsZones[1]
  properties: {
    ttl: 3600
    aRecords: [
      {
        ipv4Address: SWAPrivateEndpoint.properties.customDnsConfigs[0].ipAddresses[0]
      }
    ]
  }
}

// Deploy Cosmos DB
resource cosmosAccount 'Microsoft.DocumentDB/databaseAccounts@2023-04-15' = if (deployCosmosDB) {
  name: toLower('cosmos-vico-${uniqueName}')
  location: location
  kind: 'GlobalDocumentDB'
  properties: {
    consistencyPolicy: { defaultConsistencyLevel: 'Session' }
    locations: [ {
        locationName: location
        failoverPriority: 0
        isZoneRedundant: false
      }
    ]
    databaseAccountOfferType: 'Standard'
  }
}

resource cosmosDatabase 'Microsoft.DocumentDB/databaseAccounts/sqlDatabases@2023-04-15' = if (deployCosmosDB) {
  parent: cosmosAccount
  name: 'CopilotChat'
  properties: {
    resource: {
      id: 'CopilotChat'
    }
  }
}

resource messageContainer 'Microsoft.DocumentDB/databaseAccounts/sqlDatabases/containers@2023-04-15' = if (deployCosmosDB) {
  parent: cosmosDatabase
  name: 'chatmessages'
  properties: {
    resource: {
      id: 'chatmessages'
      indexingPolicy: {
        indexingMode: 'consistent'
        automatic: true
        includedPaths: [
          {
            path: '/*'
          }
        ]
        excludedPaths: [
          {
            path: '/"_etag"/?'
          }
        ]
      }
      partitionKey: {
        paths: [
          '/id'
        ]
        kind: 'Hash'
        version: 2
      }
    }
  }
}

resource sessionContainer 'Microsoft.DocumentDB/databaseAccounts/sqlDatabases/containers@2023-04-15' = if (deployCosmosDB) {
  parent: cosmosDatabase
  name: 'chatsessions'
  properties: {
    resource: {
      id: 'chatsessions'
      indexingPolicy: {
        indexingMode: 'consistent'
        automatic: true
        includedPaths: [
          {
            path: '/*'
          }
        ]
        excludedPaths: [
          {
            path: '/"_etag"/?'
          }
        ]
      }
      partitionKey: {
        paths: [
          '/id'
        ]
        kind: 'Hash'
        version: 2
      }
    }
  }
}

resource participantContainer 'Microsoft.DocumentDB/databaseAccounts/sqlDatabases/containers@2023-04-15' = if (deployCosmosDB) {
  parent: cosmosDatabase
  name: 'chatparticipants'
  properties: {
    resource: {
      id: 'chatparticipants'
      indexingPolicy: {
        indexingMode: 'consistent'
        automatic: true
        includedPaths: [
          {
            path: '/*'
          }
        ]
        excludedPaths: [
          {
            path: '/"_etag"/?'
          }
        ]
      }
      partitionKey: {
        paths: [
          '/id'
        ]
        kind: 'Hash'
        version: 2
      }
    }
  }
}

resource memorySourcesContainer 'Microsoft.DocumentDB/databaseAccounts/sqlDatabases/containers@2023-04-15' = if (deployCosmosDB) {
  parent: cosmosDatabase
  name: 'chatmemorysources'
  properties: {
    resource: {
      id: 'chatmemorysources'
      indexingPolicy: {
        indexingMode: 'consistent'
        automatic: true
        includedPaths: [
          {
            path: '/*'
          }
        ]
        excludedPaths: [
          {
            path: '/"_etag"/?'
          }
        ]
      }
      partitionKey: {
        paths: [
          '/id'
        ]
        kind: 'Hash'
        version: 2
      }
    }
  }
}

resource CosmosPrivateEndpoint 'Microsoft.Network/privateEndpoints@2021-08-01' = if (deployCosmosDB) {
  name: '${cosmosAccount.name}-pl'
  tags: tags
  location: location
  properties: {
    subnet: {
      id: virtualNetwork.properties.subnets[4].id
    }
    privateLinkServiceConnections: [
      {
        name: '${cosmosAccount.name}-pl'
        properties: {
          privateLinkServiceId: cosmosAccount.id
          groupIds: ['Sql'] // NoSQL - https://learn.microsoft.com/en-us/azure/cosmos-db/how-to-configure-private-endpoints#private-zone-name-mapping
        }
      }
    ]
  }
}

resource CosmosEndpointAEntry 'Microsoft.Network/privateDnsZones/A@2020-06-01' = if (deployCosmosDB) {
  name: cosmosAccount.name
  parent: privateDnsZones[2]
  properties: {
    ttl: 3600
    aRecords: [
      {
        ipv4Address: CosmosPrivateEndpoint.properties.customDnsConfigs[0].ipAddresses[0]
      }
    ]
  }
}


